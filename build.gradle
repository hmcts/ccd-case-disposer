plugins {
  id 'application'
  id 'checkstyle'
  id 'pmd'
  id 'idea'
  id 'jacoco'
  id 'io.spring.dependency-management' version '1.1.7'
  id 'org.springframework.boot' version '3.5.7'
  id 'org.owasp.dependencycheck' version '12.1.6'
  id 'com.github.ben-manes.versions' version '0.52.0'
  id 'org.sonarqube' version '7.0.1.6134'
  id 'uk.gov.hmcts.java' version '0.12.67'
  id 'io.freefair.lombok' version '8.14.2'
  id 'org.flywaydb.flyway' version '11.15.0'
  id "info.solidsoft.pitest" version '1.15.0'
  id "au.com.dius.pact" version '4.6.17'
}

group = 'uk.gov.hmcts.reform'
version = '0.0.1'

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(21)
  }
}

tasks.withType(JavaExec).configureEach {
  javaLauncher.set(javaToolchains.launcherFor(java.toolchain))
}

sourceSets {
    functionalTest {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDir file('src/functionalTest/java')
        }
        resources.srcDir file('src/functionalTest/resources')
    }

    integrationTest {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDir file('src/integrationTest/java')
        }
        resources.srcDir file('src/integrationTest/resources')
    }

  contractTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/contractTest/java')
    }
    resources.srcDir file('src/contractTest/resources')
  }
}

idea {
  module {
    testSources.from(project.sourceSets.integrationTest.java.srcDirs)
    testSources.from(project.sourceSets.functionalTest.java.srcDirs)
    testSources.from(project.sourceSets.contractTest.java.srcDirs)
    testResources.from(project.sourceSets.integrationTest.resources.srcDirs)
    testResources.from(project.sourceSets.functionalTest.resources.srcDirs)
    testResources.from(project.sourceSets.contractTest.resources.srcDirs)
  }
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:unchecked" << "-Werror"
}

tasks.withType(Test) {
    useJUnitPlatform()

    testLogging {
        exceptionFormat = 'full'
    }
}

test {
    failFast = true
}

task functional(type: Test) {
    description = "Runs functional tests"
    group = "Verification"
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
}

task integration(type: Test) {
    description = "Runs integration tests"
    group = "Verification"
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    failFast = true
    mustRunAfter test
}

task fortifyScan(type: JavaExec)  {
  mainClass = "uk.gov.hmcts.fortifyclient.FortifyClientMainApp"
  classpath += sourceSets.test.runtimeClasspath
  jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
}

task checkoutDataStoreMigrationScripts (type: Exec){
   executable "src/integrationTest/resources/scripts/checkout-datastore-db-scripts.sh"
   outputs.upToDateWhen { false }
}

//**** Uncomment this line to checkout latest data-store-api migration scripts *****
//integration.dependsOn(checkoutDataStoreMigrationScripts)

check.dependsOn integration

checkstyle {
  maxWarnings = 0
  toolVersion = '12.1.1'
  getConfigDirectory().set(new File(rootDir, 'config/checkstyle'))
}

pmd {
    //sourceSets = [sourceSets.main, sourceSets.test, sourceSets.functionalTest, sourceSets.integrationTest]
    sourceSets = []
    reportsDir = file("$project.buildDir/reports/pmd")
    // https://github.com/pmd/pmd/issues/876
    ruleSets = []
    ruleSetFiles = files("config/pmd/ruleset.xml")
}

static def getCheckedOutGitCommitHash() {
  'git rev-parse --verify --short HEAD'.execute().text.trim()
}

jacocoTestReport {
    executionData(test)
    reports {
        xml.required = true
        csv.required = false
        xml.outputLocation = file("${buildDir}/reports/jacoco/test/jacocoTestReport.xml")
    }
}

project.tasks['sonarqube'].dependsOn jacocoTestReport

sonarqube {
  properties {
    property "sonar.projectName", "Reform :: ccd-case-disposer"
    property "sonar.projectKey", "uk.gov.hmcts.reform:ccd-case-disposer"
    property "sonar.coverage.jacoco.xmlReportPaths", "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"
    property "sonar.coverage.exclusions", "**/ccd/ApplicationBootstrap.java"
    property "sonar.pitest.mode", "reuseReport"
    property "sonar.pitest.reportsDirectory", "build/reports/pitest"
    property "sonar.sources", "src/main/java"
    property "sonar.tests", "src/test/java,src/contractTest/java,src/functionalTest/java,src/integrationTest/java"
  }
}

// before committing a change, make sure task still works
dependencyUpdates {
    def isNonStable = { String version ->
        def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().contains(qualifier) }
        def regex = /^[0-9,.v-]+$/
        return !stableKeyword && !(version ==~ regex)
    }
    rejectVersionIf { selection -> // <---- notice how the closure argument is named
        return isNonStable(selection.candidate.version) && !isNonStable(selection.currentVersion)
    }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
    // Specifies if the build should be failed if a CVSS score above a specified level is identified.
    // range of 0-10 fails the build, anything greater and it doesn't fail the build
    failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
    suppressionFile = 'config/owasp/suppressions.xml'

    analyzers {
        // Disable scanning of .NET related binaries
        assemblyEnabled = false
    }
}

dependencyManagement {
    dependencies {
        dependency group: 'org.bouncycastle', name: 'bcpkix-jdk18on', version: '1.82'
        dependencySet(group: 'org.springframework.cloud', version: '4.3.0') {
            entry 'spring-cloud-starter-openfeign'
            entry 'spring-cloud-openfeign-core'
        }
        dependencySet(group: 'org.yaml', version: '2.5') {
            entry 'snakeyaml'
        }

        dependencySet(group: 'ch.qos.logback', version: '1.5.20') {
          entry 'logback-core'
          entry 'logback-classic'
        }
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
      url = 'https://pkgs.dev.azure.com/hmcts/Artifacts/_packaging/hmcts-lib/maven/v1'
    }
}

pitest {
    junit5PluginVersion.set('1.2.1')
    targetClasses = ['uk.gov.hmcts.reform.*']
    excludedClasses = [
            'uk.gov.hmcts.reform.ccd.madeup.*'
    ]
    threads = 10
    enableDefaultIncrementalAnalysis = true
    outputFormats = ['XML', 'HTML']
    timestampedReports = false
    mutationThreshold = 50
    timeoutConstInMillis = 30000
    mutators.set([
            'CONDITIONALS_BOUNDARY',
            'INCREMENTS',
            'INVERT_NEGS',
            'MATH',
            'NEGATE_CONDITIONALS',
            'VOID_METHOD_CALLS',
            'EMPTY_RETURNS',
            'FALSE_RETURNS',
            'NULL_RETURNS',
            'PRIMITIVE_RETURNS',
            'TRUE_RETURNS',
            'REMOVE_CONDITIONALS'
    ])
}

project.tasks['pitest'].group = "Verification"

def versions = [
    apiGuardian         : '1.1.2',
    commonsIo           : '2.20.0',
    elasticsearch       : '8.19.4',
    flyway              : '11.15.0',
    gradlePitest        : '1.15.0',
    idamClient          : '3.0.4',
    junit               : '5.13.4',
    lombok              : '1.18.42',
    reformLogging       : '6.1.9',
    restAssured         : '5.5.6',
    s2sClient           : '5.3.3',
    sonarPitest         : '0.5',
    springBoot          : springBoot.class.package.implementationVersion,
    testcontainers      : '1.21.3',
    wiremock            : '3.0.1',
    pactVersion         : '4.6.17',
    serenity            : '3.1.20',
]

ext['jackson.version'] = '2.16.0'
project.ext.pacticipantVersion = getCheckedOutGitCommitHash()

rootProject.tasks.named("processTestResources") {
  duplicatesStrategy = 'include'
}

rootProject.tasks.named("processFunctionalTestResources") {
  duplicatesStrategy = 'include'
}

rootProject.tasks.named("processIntegrationTestResources") {
  duplicatesStrategy = 'include'
}
rootProject.tasks.named("processContractTestResources") {
  duplicatesStrategy = 'include'
}

dependencies {

    modules {
      module("com.google.collections:google-collections") {
        replacedBy("com.google.guava:guava", "google-collections is part of guava")
      }
    }

    implementation group: 'commons-io', name: 'commons-io', version: versions.commonsIo
    implementation group: 'io.rest-assured', name: 'rest-assured', version: versions.restAssured
    implementation group: 'jakarta.ws.rs', name: 'jakarta.ws.rs-api', version: '4.0.0'
    implementation group: 'jakarta.annotation', name: 'jakarta.annotation-api', version: '3.0.0'
    implementation group: 'jakarta.inject', name: 'jakarta.inject-api', version: '2.0.1'
    implementation group: 'com.github.hmcts', name: 'idam-java-client', version: versions.idamClient
    implementation group: 'com.github.hmcts', name: 'service-auth-provider-java-client', version: versions.s2sClient
    implementation group: 'com.github.hmcts.java-logging', name: 'logging', version: versions.reformLogging
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.13.2'
    implementation group: 'com.google.guava', name: 'guava', version: '33.5.0-jre'
    implementation group: 'commons-fileupload', name: 'commons-fileupload', version: '1.6.0'
    implementation group: 'commons-net', name: 'commons-net', version: '3.12.0'

    implementation group: 'org.springframework.boot', name: 'spring-boot-starter'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-jdbc'
    implementation group: 'net.logstash.logback', name: 'logstash-logback-encoder', version: '8.1'
    implementation group: 'com.microsoft.azure', name: 'applicationinsights-web', version: '3.7.6'
    implementation group: 'ch.qos.logback.access', name: 'common', version: '2.0.3'

    implementation group: 'org.postgresql', name: 'postgresql', version: '42.7.8'
    implementation group: 'org.projectlombok', name: 'lombok', version: versions.lombok
    implementation group: 'co.elastic.clients', name: 'elasticsearch-java', version: versions.elasticsearch

    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok
    testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok

    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'
    testImplementation group: 'org.testcontainers', name: 'postgresql', version: versions.testcontainers
    testImplementation group: 'org.testcontainers', name: 'junit-jupiter', version: versions.testcontainers
    testImplementation group: 'org.testcontainers', name: 'elasticsearch', version: versions.testcontainers
    testImplementation group: 'org.awaitility', name: 'awaitility', version: '4.3.0'
    testImplementation group: 'com.github.tomakehurst', name: 'wiremock-jre8-standalone', version: versions.wiremock
    testImplementation group: 'org.flywaydb', name: 'flyway-core', version: versions.flyway
    testImplementation group: 'com.pivovarit', name: 'throwing-function', version: '1.6.1'
    testImplementation group: 'info.solidsoft.gradle.pitest', name: 'gradle-pitest-plugin', version: versions.gradlePitest
    testImplementation 'org.codehaus.sonar-plugins:sonar-pitest-plugin:0.5'
    testImplementation 'com.github.hmcts:fortify-client:1.4.10:all'
    testImplementation group: 'io.github.hakky54', name: 'logcaptor', version: '2.12.1'

    testImplementation group: 'org.flywaydb', name: 'flyway-database-postgresql', version: versions.flyway

    integrationTestImplementation sourceSets.main.runtimeClasspath
    integrationTestImplementation sourceSets.test.runtimeClasspath

    functionalTestImplementation sourceSets.main.runtimeClasspath
    functionalTestImplementation sourceSets.test.runtimeClasspath

    functionalTestCompileOnly group: 'org.apiguardian', name: 'apiguardian-api', version: versions.apiGuardian
    integrationTestCompileOnly group: 'org.apiguardian', name: 'apiguardian-api', version: versions.apiGuardian

    //pact contract testing
    contractTestImplementation group: 'au.com.dius.pact.consumer', name: 'junit5', version: versions.pactVersion
    contractTestImplementation group: 'au.com.dius.pact.consumer', name: 'junit', version: versions.pactVersion
    contractTestRuntimeOnly(group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: versions.junit)
    contractTestImplementation(group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: versions.junit)

    contractTestImplementation sourceSets.main.runtimeClasspath
    contractTestImplementation sourceSets.test.runtimeClasspath
}

application {
  mainClass = 'uk.gov.hmcts.reform.ccd.ApplicationBootstrap'
}


pact {
  broker {
    pactBrokerUrl = System.getenv("PACT_BROKER_FULL_URL") ?: 'http://localhost:80'
  }
  publish {
    pactDirectory = 'build/pacts'
    tags = [System.getenv("PACT_BRANCH_NAME") ?: 'Dev']
    version = project.pacticipantVersion
  }
}

task runAndPublishConsumerPactTests(type: Test){
  logger.lifecycle("Runs pact Tests")
  testClassesDirs = sourceSets.contractTest.output.classesDirs
  classpath = sourceSets.contractTest.runtimeClasspath
  finalizedBy(pactPublish)
}

task contract(type: Test, description: 'Runs the pact contract tests.', group: 'Verification') {
  description = "Runs the consumer Pact tests"
  useJUnitPlatform()
  testClassesDirs = sourceSets.contractTest.output.classesDirs
  classpath = sourceSets.contractTest.runtimeClasspath
}

task runProviderPactVerification(type: Test) {
  logger.lifecycle("Runs provider pact Tests")
  testClassesDirs = sourceSets.contractTest.output.classesDirs
  classpath = sourceSets.contractTest.runtimeClasspath
  systemProperty 'pact.verifier.publishResults', System.getProperty('pact.verifier.publishResults')
  systemProperty 'pact.provider.version', project.pacticipantVersion
}

runProviderPactVerification.dependsOn contract
runProviderPactVerification.finalizedBy pactVerify

bootJar {
    getArchiveFileName().set(provider {
        'ccd-case-disposer.jar'
    })

    manifest {
        attributes('Implementation-Version': project.version.toString())
    }
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}
